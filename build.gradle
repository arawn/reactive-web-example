buildscript {
    ext {
        springBootVersion = '2.0.0.BUILD-SNAPSHOT'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    ext {
        versions = [
            jdk                : '1.8'
        ]
    }

    configurations {
        all*.exclude module: "tomcat-jdbc"

        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/snapshot" }
    }

    sourceCompatibility = versions.jdk
    targetCompatibility = versions.jdk
    compileJava.options.encoding = 'UTF-8'
}

project('data-service') {
    dependencies {
        compile('org.springframework.boot:spring-boot-starter-webflux') {
            exclude module: 'spring-boot-starter-tomcat'
        }

        compile 'io.undertow:undertow-core'
        // compile 'io.projectreactor.ipc:reactor-netty'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot.experimental:spring-boot-dependencies-web-reactive:0.1.0.BUILD-SNAPSHOT"
        }
    }

    bootRepackage {
        executable = true
        excludeDevtools = true
    }
}

project('notification-server') {

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile 'org.springframework.boot:spring-boot-starter-undertow'

        compile 'io.projectreactor:reactor-core:3.0.4.RELEASE'

        compile 'org.apache.httpcomponents:httpclient'
        compile 'org.apache.httpcomponents:httpasyncclient'
        compile 'io.netty:netty-all'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    bootRepackage {
        executable = true
        excludeDevtools = true
    }

}

project('notification-client') {

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-jersey') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile('org.glassfish.jersey.media:jersey-media-sse:2.25.1')

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    bootRepackage {
        executable = true
        excludeDevtools = true
    }

}

wrapper {
    gradleVersion = '3.1'
}